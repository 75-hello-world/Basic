!!
This program demonstrates using
Bluetooth as both an application
that listens for a connection
or makes a connection to a listener.

Before running this program use
the Android "Settings" application
to enable Bluetooth and to pair
with the device(s) that you will
talk to.

This program will send and receive
data bytes to and from a connected
device.

You can use this program to 
(among many other things) set
up a chat between two Android
devices.

Note: The UUID used by default is
the standard serial port UUID. This
can be changed. Read the manual.

!!

! Begin by opening Bluetooth
! If Bluetooth is not enabled
! the program will stop here.

BT.OPEN

! When BT is opened, the program
! will start listening for another
! device to connect. At this time
! the user can continue to wait
! for a connection are can attempt
! to connect to another device
! that is waiting for a connection

! Ask user what to do

ARRAY.LOAD type$[], "Connect to listener", "Continue to listen for connection", "Quit"
msg$ = "Select operation mode"

new_connection:

SELECT type, type$[], msg$

! If the user pressed the back
! key or selected quit then quit
! otherwise try to connect to
! a listener

IF (type = 0) | (type =3)
 PRINT "Thanks for playing"
 BT.CLOSE
 END
ELSEIF type = 1
 BT.CONNECT
ENDIF

! Read status until
! a connection is made


DO
 BT.STATUS s
 IF s = 1
  PRINT "Listening
 ELSEIF s =2
  PRINT "Connecting"
 ELSEIF s = 3
  PRINT "Connected"
 ENDIF
 PAUSE 1000

UNTIL s =3

! When a connection is made
! get the name of the connected
! device

BT.DEVICE.NAME device$

! *** Read/Write Loop ****

RW_Loop:

DO

 ! Read status to insure
 ! that we remain connected.
 ! If disconnected, program
 ! reverts to listen mode.
 ! In that case, ask user
 ! what to do.

 BT.STATUS s
 IF s<> 3
  PRINT "Connection lost"
  GOTO new_connection
 ENDIF

 ! Read messages until
 ! the message queue is
 ! empty

 DO
  BT.READ.READY rr
  IF rr
   BT.READ.BYTES msg$
   PRINT device$;": ";msg$
  ENDIF
 UNTIL rr = 0

 ! Message Queue is empty
 ! Send a message
 ! Note: If no text is entered
 ! then no text will be sent

 INPUT "Text to send", msg$
 IF (msg$ <> "") & (msg$<> "*")
  BT.WRITE msg$
  PRINT "Me: "; msg$
 ENDIF

UNTIL msg$ = "*"

! if msg$ is "*" then close connection
! and restart

BT.CLOSE
BT.OPEN
GOTO new_connection
